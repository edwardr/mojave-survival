scn ArcadeScript

int Waiting							; 0 = Not waiting, 1 = Waiting
int CombatStyleMelee			; 0 = Not melee, 1= Melee
int CombatStyleRanged			; 0 = Not ranged, 1 = Ranged
int IsFollowingDefault			; 0 = Not following default, 1 = Is following default range
int IsFollowingLong				; 0 = Not following long, 1 = Is following long
int FollowerSwitchAggressive	; 0 = Passive (wait on player), 1 = Aggressive (attack when see enemies)

; RASM - some setup to use antivenom and stimpaks
short healCheckTimerStart
float healCheckTimer
short iteration

;---------------------------------------------------------------------------------------------------------------------------------------------


begin OnDeath

	if ( VNPCFollowers.ArcadeHired == 1 )
		set VNPCFollowers.nCurrentFollowers to VNPCFollowers.nCurrentFollowers - 1		; <<< (Added 7_10_10) Arcade needed "nCurrentFollowers" set so one of Victor's...
		if (VNPCFollowers.nCurrentFollowers == 0)														; <<< ...dialogue's would acknowledge his death properly (used template from Boone) -ETB
			set VNPCFollowers.bPlayerHasFollower to 0												; <<<
		endif																										; <<<
		set VNPCFollowers.ArcadeHired to 0
		;Player.RemoveFromFaction VArcadeFaction <Note: There is no "VArcadeFaction" (only "FollowersOfTheApocalypseFaction") for him -- Does he need his own? (Boone has) -- Didn't remove Player from Apoc faction because other NPCs are in it.> -ETB
		set VNPCFollowers.bHumanoidInParty to 0
		ShowMessage FollowerMessageDeadArcade
		player.RemovePerk BetterHealing
		ShowMessage FollowerMessagePerkArcadeRemove
	endif

	set VNPCFollowers.bArcadeDead to 1

	; Fail VMS54 if it's not completed since you need Arcade around.
	if (GetQuestCompleted VMS54 == 0)
		SetStage VMS54 110
	endif

	; Remove VMS35 objective to enslave Arcade if it's open but not complete
	if (GetObjectiveDisplayed VMS35 50 == 1) && (GetObjectiveCompleted VMS35 50 == 0)
		SetObjectiveDisplayed VMS35 50 0
	endif
	
end

;---------------------------------------------------------------------------------------------------------------------------------------------

begin GameMode
	if GetPlayerTeammate == 1
	if ArcadeREF.isInCombat == 1
		if healCheckTimerStart == 0 
			set healCheckTimer to 10
			set healCheckTimerStart to 1
		else
			if healCheckTimer >= 0
				set healCheckTimer to healCheckTimer - getSecondsPassed
			else
				if ArcadeREF.GetHealthPercentage <= 0.50
					if ArcadeREF.getItemCount stimpak >= 1
						ArcadeREF.equipItem stimpak
					endif
				endif
				if ArcadeREF.getIsPoisoned == 1
					if ArcadeREF.getItemCount NVAntivenom >= 1
						ArcadeREF.equipItem NVAntivenom
					endif
				endif

				set healCheckTimerStart to 0
			endif
		endif
	endif
	endif
end

begin OnCombatEND

	if GetPlayerTeammate == 1
		resethealth
		restoreav perceptioncondition 100
		restoreav endurancecondition 100
		restoreav leftattackcondition 100
		restoreav leftmobilitycondition 100
		restoreav rightattackcondition 100
		restoreav rightmobilitycondition 100
		if ArcadeREF.getIsPoisoned == 1
			if ArcadeREF.getItemCount NVAntivenom >= 1
				ArcadeREF.equipItem NVAntivenom
			endif
		endif
	endif

end


BEGIN OnLoad

	;Script to make sure we set the right combat style on loading.
	If (CombatStyleMelee == 1)
		ArcadeREF.SetCombatStyle FollowersCombatStyleMelee;
	Else
		ArcadeREF.SetCombatStyle FollowersCombatStyleRanged;
	EndIf

END